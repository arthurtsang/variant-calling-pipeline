rule realign:
  input: 
    "output/filtered_indels.vcf",
    "output/filtered_snps.vcf"
  output:
    touch( "output/realigned" )

rule call_variants_1:
  """Call Variants
  First round of variant calling. The variants identified in this step will be filtered and provided as input for Base Quality Score Recalibration
  """
  conda: "conda.yml"
  input: "output/dedup_reads.bam"
  output: "output/raw_variants.vcf"
  shell: "gatk HaplotypeCaller -R {REF} -I {input} -O {output}"

rule extract_snps_1:
  """Extract SNPs
  This step separates SNPs so they can be processed and used independently
  """
  conda: "conda.yml"
  input: rules.call_variants_1.output
  output: "output/raw_snps.vcf"
  shell: "gatk SelectVariants -R {REF} -V {input} -select-type SNP -O {output}"

rule extract_indels_1:
  """Extract Indels
  This step separates Indels so they can be processed and used independently
  """
  conda: "conda.yml"
  input: rules.call_variants_1.output
  output: "output/raw_indels.vcf"
  shell: "gatk SelectVariants -R {REF} -V {input} -select-type INDEL -O {output}"

rule filter_snps_1:
  conda: "conda.yml"
  input: rules.extract_snps_1.output
  output: "output/filtered_snps.vcf"
  shell:
    """
      gatk VariantFiltration -R {REF} -V {input} --filter-name "QD_filter" --filter "QD < 2.0" --filter-name "FS_filter" --filter "FS > 60.0" --filter-name "MQ_filter" --filter "MQ < 40.0" --filter-name "SOR_filter" --filter "SOR > 4.0" -O {output}
    """

rule filter_indel_1:
  conda: "conda.yml"
  input: rules.extract_indels_1.output
  output: "output/filtered_indels.vcf"
  shell: 
    """
      gatk VariantFiltration -R {REF} -V {input} --filter-name "QD_filter" --filter "QD < 2.0" --filter-name "FS_filter" --filter "FS > 200.0" --filter-name "SOR_filter" --filter "SOR > 10.0" -O {output}
    """

