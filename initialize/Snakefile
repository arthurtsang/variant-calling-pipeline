from snakemake.remote.FTP import RemoteProvider as FTPRemoteProvider
FTP = FTPRemoteProvider()

rule initialize:
  input:
    expand( FASTQDIR+"/"+EXPERIMENT+"_{read}.fastq.gz", read = ["1", "2"]),
    expand( REFDIR+"/Saccharomyces_cerevisiae.R64-1-1.dna.toplevel.fa.{extension}", extension = ["amb", "ann", "bwt", "pac", "sa", "fai" ]),
    REFDIR + "/Saccharomyces_cerevisiae.R64-1-1.dna.toplevel.dict"
  output: 
    touch("output/initialized")

rule download_fastq:
  """Download fastq from https://www.ebi.ac.uk/ena/data/view/SRX4093111"""
  input: FTP.remote(FASTQURL+"/"+EXPERIMENT+"/"+EXPERIMENT+"_{read}.fastq.gz", keep_local=True )
  output: FASTQDIR+"/"+EXPERIMENT+"_{read}.fastq.gz"
  run: 
    shell( "mv {input} {FASTQDIR}" )

rule download_ref:
  """Download ref from http://uswest.ensembl.org/Saccharomyces_cerevisiae/Info/Index"""
  input: FTP.remote(REFURL, keep_local=True)
  output: REF
  run: 
    shell( "gzip -d {input}  > {output}" )

rule bwa_index_ref:
  """Prepare ref for use with BWA, create index"""
  input: REF
  output: expand( "{dir}/Saccharomyces_cerevisiae.R64-1-1.dna.toplevel.fa.{extension}", dir = [ REFDIR ], extension = ["amb", "ann", "bwt", "pac", "sa" ])
  conda: "conda.yml"
  shell: "bwa index {REF}"

rule fasta_file_index_ref:
  """Prepare ref for use with GATK, create FASTA file index"""
  input: REF
  output: REFDIR + "/Saccharomyces_cerevisiae.R64-1-1.dna.toplevel.fa.fai"
  conda: "conda.yml"
  shell: "samtools faidx {REF}"

rule sequence_dictionary_ref:
  """Prepare ref for use with GATK, create sequence dictionary"""
  input: REF
  output: REFDIR + "/Saccharomyces_cerevisiae.R64-1-1.dna.toplevel.dict"
  conda: "conda.yml"
  shell: "picard CreateSequenceDictionary R={input} O={output}"

